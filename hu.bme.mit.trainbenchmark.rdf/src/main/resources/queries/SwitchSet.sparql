PREFIX base: <http://www.semanticweb.org/ontologies/2015/trainbenchmark#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?semaphore ?route ?swP ?sw
WHERE
{
    ?semaphore rdf:type base:Semaphore .
    ?route rdf:type base:Route .
    ?swP rdf:type base:SwitchPosition .
    ?sw rdf:type base:Switch .

    ?route base:entry ?semaphore .
    ?route base:follows ?swP .
    ?swP base:switch ?sw .

    ?semaphore base:signal base:SIGNAL_GO .
    ?sw base:currentPosition ?swStateSW .
    ?swP base:position ?swStateSWP .

    FILTER (!sameTerm(?swStateSW, ?swStateSWP))
}
